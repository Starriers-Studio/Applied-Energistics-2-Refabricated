/*
 * This file is part of Applied Energistics 2.
 * Copyright (c) 2013 - 2015, AlgorithmX2, All rights reserved.
 *
 * Applied Energistics 2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Applied Energistics 2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Applied Energistics 2.  If not, see <http://www.gnu.org/licenses/lgpl>.
 */

buildscript {
    dependencies {
        // Fix for the shadow plugin using outdated ASM
        classpath 'org.ow2.asm:asm:9.5'
        classpath 'org.ow2.asm:asm-commons:9.5'
    }
}

plugins {
    id "fabric-loom"
    id "maven-publish"
    id "com.diffplug.spotless"
    id "io.github.goooler.shadow"
    id "de.undercouch.download"
}

apply plugin: ProjectDefaultsPlugin

base {
    archivesName = "appliedenergistics2"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/flatbuffers/generated'
        }
        resources {
            srcDir 'src/generated/resources'
        }
    }
    buildtools
}

configurations {
    localRuntimeOnly
    shaded {
        transitive = false
    }
    buildtoolsImplementation.extendsFrom(compileClasspath)
    // Dependencies only used for the guide export, but not shipped
    guideExportOnly
    configurations.compileClasspath.extendsFrom(guideExportOnly)
    configurations.runtimeClasspath.extendsFrom(guideExportOnly)

    runtimeClasspath.extendsFrom localRuntimeOnly
}

import appengbuild.*
import com.diffplug.gradle.spotless.JsonExtension

dependencies {
    // To be copied into the jar file
    shaded project(path: ':libs:markdown', configuration: "archives")
    shaded "io.methvin:directory-watcher:${directory_watcher_version}"
    shaded "org.yaml:snakeyaml:${snakeyaml_version}"
    shaded "com.google.flatbuffers:flatbuffers-java:${flatbuffers_version}"

    implementation(project(':libs:markdown')) {
        transitive = false
    }
    // Do not inherit any transitive dependencies here since we rely on those dependencies being
    // present in Minecrafts own dependencies already.
    implementation("io.methvin:directory-watcher:${directory_watcher_version}") {
        transitive = false
    }
    implementation("org.yaml:snakeyaml:${snakeyaml_version}") {
        transitive = false
    }
    implementation("com.google.flatbuffers:flatbuffers-java:${flatbuffers_version}") {
        transitive = false
    }
    // Used for the guide export
    guideExportOnly("org.bytedeco:ffmpeg-platform:${ffmpeg_version}")

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"

    // compile against provided APIs
    modCompileOnly("dev.emi:emi-fabric:${emi_version}:api") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }
    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${project.wthit_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }
    modCompileOnly("maven.modrinth:jade:${project.jade_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }

    switch (project.runtime_itemlist_mod) {
        case "emi":
            modLocalRuntime("dev.emi:emi-fabric:${emi_version}") {
                exclude group: "net.fabricmc"
                exclude group: "net.fabricmc.fabric-api"
            }
            break
        case "rei":
            modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
                exclude group: "net.fabricmc"
                exclude group: "net.fabricmc.fabric-api"
            }
            break
    }

    switch (project.runtime_tooltip_mod) {
        case "wthit":
            modLocalRuntime("mcp.mobius.waila:wthit:fabric-${project.wthit_version}") {
                exclude group: "net.fabricmc"
                exclude group: "net.fabricmc.fabric-api"
            }
            break
        case "jade":
            modLocalRuntime("maven.modrinth:jade:${project.jade_version}") {
                exclude group: "net.fabricmc"
                exclude group: "net.fabricmc.fabric-api"
            }
            break
    }

    if (project.runtime_trinkets == "true") {
        modLocalRuntime("dev.emi:trinkets:${project.trinkets_version}") {
            exclude group: "net.fabricmc"
            exclude group: "net.fabricmc.fabric-api"
        }
    }

    // Fabric API.
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
    include modImplementation("teamreborn:energy:${project.energy_version}") {
        exclude group: "net.fabricmc"
        exclude group: "net.fabricmc.fabric-api"
    }

    // unit test dependencies
    testImplementation(platform("org.junit:junit-bom:${project.junit_version}"))
    testImplementation(platform("org.assertj:assertj-bom:${project.assertj_version}"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.platform:junit-platform-launcher")
    testImplementation("org.assertj:assertj-core")
    testImplementation("com.google.guava:guava-testlib:21.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.12.0")
    //testImplementation("net.neoforged:testframework:${project.neoforge_version}")

    compileOnly 'org.apache.commons:commons-configuration2:2.9.0'

    buildtoolsImplementation 'de.siegmar:fastcsv:2.1.0'
    buildtoolsImplementation 'com.google.code.gson:gson:2.8.9'
}

allprojects {
    repositories {
        maven {
            url "https://maven.shedaniel.me/"
            content {
                includeGroup "me.shedaniel"
                includeGroup "me.shedaniel.cloth"
                includeGroup "dev.architectury"
            }
        }
        maven {
            url "https://maven2.bai.lol"
            content {
                includeGroup "mcp.mobius.waila"
                includeGroup "lol.bai"
            }
        }
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/"
            content {
                includeGroup "dev.emi"
            }
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
        maven {
            url "https://maven.parchmentmc.net/"
            content {
                includeGroup "org.parchmentmc.data"
            }
        }
        maven { // for TOP
            url "https://maven.k-4u.nl/"
            content {
                includeGroup "mcjty.theoneprobe"
            }
        }
        maven {
            url = "https://maven.theillusivec4.top/"
            content {
                includeGroup "top.theillusivec4.curios"
            }
        }
        maven {
            url = "https://maven.blamejared.com/"
            content {
                includeGroup "mezz.jei"
            }
        }
        mavenCentral()
    }
}

///////////////////
// Version Number
ext.tag = System.getenv('TAG') ?: ""
if (ext.tag) {
    if (!ext.tag.startsWith("fabric/v")) {
        throw new GradleException("Tags for the fabric version should start with fabric/: ${ext.tag}")
    }

    project.version = ext.tag.substring("fabric/v".length())
} else {
    // This essentially tries to implement delay expansion of the project version based on a value source to
    // still support configuration caching.
    project.version = providers.gradleProperty("version").orElse("").flatMap(version -> {
        if (!version.isBlank()) {
            return version;
        }
        return providers.of(ProjectVersionSource.class, spec -> {
            spec.getParameters().getDefaultBranches().addAll("main", "fabric/" + project.minecraft_version)
        });
    }).get()
}

tasks.register("printProjectVersion", PrintProjectVersion.class);

test {
    useJUnitPlatform()

    // Might not need this anymore...
    systemProperty "guideDev.ae2guide.sources", file("guidebook").absolutePath
}

dependencies {
    buildtoolsImplementation 'de.siegmar:fastcsv:2.1.0'
    buildtoolsImplementation 'com.google.code.gson:gson:2.8.9'
}

loom {
    accessWidenerPath = file("src/main/resources/ae2.accesswidener")

    runs {
        client {
            client()
            property("appeng.tests", "true")
            property("guideDev.ae2guide.sources", file("guidebook").absolutePath)
        }
        gametestWorld {
            client()
            programArgs "--username", "AE2Dev", "--quickPlaySingleplayer", "GametestWorld"
            property("appeng.tests", "true")
            property("guideDev.ae2guide.sources", file("guidebook").absolutePath)
        }
        guide {
            client()
            property("guideDev.ae2guide.sources", file("guidebook").absolutePath)
            property("guideDev.ae2guide.startupPage", "ae2:index.md")
        }
        server {
            server()
        }
        guideexport {
            client()
            property("appeng.runGuideExportAndExit", "true")
            property("appeng.guideExportFolder", file("$buildDir/guide").absolutePath)
            property("guideDev.ae2guide.sources", file("guidebook").absolutePath)
            property("appeng.version", project.version)
        }
        // Use to run the tests
        gametest {
            server()
            name "gameTestServer"
            property "appeng.tests", "true"
            vmArg "-Dfabric-api.gametest"
            runDir = project.file("build/gametest")
        }
    }
}

//////////////
// Artifacts
processResources {
    exclude '.cache'

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    finalizedBy shadowJar
    archiveClassifier = "plain"
}

shadowJar {
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir

    from('guidebook') {
        into 'assets/ae2/ae2guide'
    }

    relocate "io.methvin", "appeng.shaded.methvin"
    relocate "org.yaml.snakeyaml", "appeng.shaded.snakeyaml"

    configurations = [project.configurations.shaded]
    archiveClassifier = null
}

assemble.dependsOn shadowJar

def publicApiIncludePatterns = {
    exclude "**/*Internal.*"
    exclude "**/*Internal\$*.*"
    include "appeng/api/**"
}

javadoc {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath + sourceSets.main.output

    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}
javadoc publicApiIncludePatterns

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

tasks.register('apiJar', Jar) {
    archiveClassifier = "api"
    // api jar ist just a development aid and serves as both a binary and source jar simultaneously
    from sourceSets.main.output
    from sourceSets.main.allJava
    manifest {
        attributes("Fabric-Loom-Remap": true)
    }
}
apiJar publicApiIncludePatterns

artifacts {
    archives javadocJar
    archives sourcesJar
    archives apiJar
}

//////////////////
// Maven publish
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "appliedenergistics2"
            version = project.version

            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            name = "Local"
            url = file("build/repo").toURI()
        }
    }
}

/////////////
// Spotless
spotless {

    java {
        target 'src/*/java/appeng/**/*.java'

        endWithNewline()
        indentWithSpaces()
        removeUnusedImports()
        toggleOffOn()
        eclipse().configFile 'codeformat/codeformat.xml'
        importOrderFile 'codeformat/ae2.importorder'

        // courtesy of diffplug/spotless#240
        // https://github.com/diffplug/spotless/issues/240#issuecomment-385206606
        custom 'noWildcardImports', {
            if (it.contains('*;\n')) {
                throw new Error('No wildcard imports allowed')
            }

            it
        }
        bumpThisNumberIfACustomStepChanges(1)
    }

   json {
        target 'src/*/resources/**/*.json'
        targetExclude 'src/generated/resources/**'
        var biomeConfig = it.new JsonExtension.BiomeJson(null)
        try {
            biomeConfig.downloadDir(new File(rootDir, ".gradle/biome").absolutePath)
        } catch (Exception ignored) {
        }
        addStep(biomeConfig.createStep())
        indentWithSpaces(2)
        endWithNewline()
    }
}

////////////////
// Crowdin
tasks.register('uploadToCrowdin', JavaExec) {
    classpath = sourceSets.buildtools.runtimeClasspath
    mainClass = 'Crowdin'
    args 'upload_source'
    workingDir "."
}
tasks.register('uploadTranslations', JavaExec) {
    classpath = sourceSets.buildtools.runtimeClasspath
    mainClass = 'Crowdin'
    args 'upload_translations'
    workingDir "."
}
tasks.register('downloadFromCrowdin', JavaExec) {
    classpath = sourceSets.buildtools.runtimeClasspath
    mainClass = 'Crowdin'
    args 'update_translations'
    workingDir "."
}

check.dependsOn tasks.register('validateResources', JavaExec) {
    group = "verification"
    classpath = sourceSets.buildtools.runtimeClasspath
    mainClass = 'ValidateResourceIds'
    workingDir "."
    args "guidebook"
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

/**
 * This task download the FlatBuffers compiler suitable for the current platform.
 */
tasks.register('downloadFlatbufferCompiler', Download) {
    var os = System.getProperty("os.name").toLowerCase(Locale.ROOT)
    var arch = System.getProperty("os.arch")
    if (os.startsWith("windows")) {
        src "https://github.com/google/flatbuffers/releases/download/v${flatbuffers_version}/Windows.flatc.binary.zip"
    } else if (os.startsWith("mac")) {
        if (arch == "aarch64") {
            src "https://github.com/google/flatbuffers/releases/download/v${flatbuffers_version}/Mac.flatc.binary.zip"
        } else if (arch == "amd64") {
            src "https://github.com/google/flatbuffers/releases/download/v${flatbuffers_version}/MacIntel.flatc.binary.zip"
        } else {
            throw new GradleException("Unknown Mac architecture: $arch")
        }
    } else if (os.startsWith("linux")) {
        src "https://github.com/google/flatbuffers/releases/download/v${flatbuffers_version}/Linux.flatc.binary.clang++-12.zip"
    } else {
        throw new GradleException("Unknown OS: $os")
    }
    onlyIfModified true
    dest layout.buildDirectory.file("flatc.zip")
}

tasks.register('downloadAndUnzipFlatbufferCompiler', Copy) {
    dependsOn downloadFlatbufferCompiler
    from zipTree(downloadFlatbufferCompiler.dest)
    into "${project.buildDir}/flatbuffers-bin"
}
/**
 * Update the generated Java Code for our scene export schema.
 * The code is checked in so this only needs to be run when the schema changes.
 */
tasks.register('updateFlatbufferSources', Exec) {
    group 'build'
    dependsOn downloadAndUnzipFlatbufferCompiler
    workingDir project.projectDir
    commandLine "${project.buildDir}/flatbuffers-bin/flatc",
            "--gen-mutable",
            "--java-package-prefix", "appeng.flatbuffers",
            "--gen-generated",
            "--java",
            "-o",
            "${project.projectDir}/src/main/flatbuffers/generated",
            "${project.projectDir}/src/main/flatbuffers/scene.fbs"

    doFirst {
        file("${project.projectDir}/src/main/flatbuffers/generated").deleteDir()
    }
    doLast {
    }
    doLast {
        // sadly flatc uses an outdated annotation
        def folder = file("${project.projectDir}/src/main/flatbuffers/generated")
        folder.eachFileRecurse(groovy.io.FileType.FILES) { file ->
            String content = file.text
            if (content.contains('@javax.annotation.Generated')) {
                content = content.replace('@javax.annotation.Generated', '@javax.annotation.processing.Generated')
                file.write(content)
            }
        }
    }
}

/**
 * Generate the TypeScript sources for our schema. The sources are manually copied
 * over to the website repository.
 */
tasks.register('updateFlatbufferTypescriptSources', Exec) {
    group 'build'
    dependsOn downloadAndUnzipFlatbufferCompiler
    workingDir project.projectDir
    commandLine "${project.buildDir}/flatbuffers-bin/flatc",
            "--ts-flat-files",
            "--ts",
            "-o",
            "${project.buildDir}/scene-ts",
            "${project.projectDir}/src/main/flatbuffers/scene.fbs"

    doFirst {
        file("${project.buildDir}/scene-ts").deleteDir()
    }
}
